class ElpGame {

    static ElpGame instance;
    field Dino dino;
    field Random random;
    field Array cactii;
    field int cactiilen;

    constructor ElpGame new() {
        var int i, cactusX;

	    do Screen.clearScreen();
        let dino = Dino.new(2, 220);


        let random = Random.new(42);

        let cactiilen = 5;
        let cactii = Array.new(cactiilen);
        let i = 0;
        let cactusX = 7;
        while (i < cactiilen) {
            let cactii[i] = Cactus.new(cactusX, 220, random);
            let cactusX = cactusX + random.next(7);
            let i = i + 1;
        }
        
        return this;
    }

    method void dispose() {
	    do dino.dispose();
        do cactii.dispose();
        do Memory.deAlloc(this);
        return;
    }

    function void newInstance() {
        let instance = ElpGame.new();
        return;
    }
    
    function ElpGame getInstance() {
        return instance;
    }

    method void run() {
        var int i;
        var char key;
        var Cactus cactus;
        while (true) {
            let key = Keyboard.keyPressed();

            while (key = 0) {
                let key = Keyboard.keyPressed();
                do Screen.clearScreen();
                do Screen.drawRectangle(0, 236, 512, 238);

                do dino.tick();
                do dino.draw();

                let i = 0;
                while (i < cactiilen) {
                    let cactus = cactii[i];
                    do cactus.draw();
                    do cactus.move(-1, 0);
                    let i = i + 1;
                }

                do Sys.wait(40);
            }

            if (~(key = 0)) { do dino.jump(); }
        }
        return;
    }
}