// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 *  A library of commonly used mathematical functions.
 *  All functions runs in O(n), where n is the number of bits used
 *  for representing a two's complement integer value (16 in the Hack computer). 
 *  Note: Jack compilers implement multiplication and division
 *  using calls to OS functions in this class.
 */
class Math {
    static int n;             // Number of bits used for representing a two's complement integer
    static Array powersOfTwo; // Stores 2^0, 2^1, 2^2,..., 2^(n-1)

    // Initializes the Math library.
    function void init() {
        var int i, pow2;
        let n = 16;
        let powersOfTwo = Array.new(n);
        let i = 0;
        let pow2 = 1;
        while (i < n) {
            let powersOfTwo[i] = pow2;
            let i = i + 1;
            let pow2 = pow2 + pow2;
        }
        return;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*'
     *  in an expression, it handles it by invoking this method. 
     *  Thus, in Jack, x * y and Math.multiply(x,y) return the same value. */
    function int multiply(int x, int y) {
        var int sum, xShifted, i;
        let sum = 0;
        let xShifted = x;
        let i = 0;
        while (i < n) {
            if (~((y & powersOfTwo[i]) = 0)) {
                let sum = sum + xShifted;
            }
            let xShifted = xShifted + xShifted;
            let i = i + 1;
        }

        return sum;
    }

    /** Returns the integer part of x / y.
     *  When a Jack compiler detects the division operator '/'
     *  an an expression, it handles it by invoking this method.
     *  Thus, x/y and Math.divide(x,y) return the same value. */
    function int divide(int x, int y) {
        var int quotient, twoQuotient, ret, xlt0, ylt0;

        let xlt0 = x < 0;
        let ylt0 = y < 0;
        let x = Math.abs(x);
        let y = Math.abs(y);

        if ((y > x) | (y < 0)) {
            return 0;
        }

        let quotient = x / (y + y);
        let twoQuotient = quotient + quotient;

        if ((x - (twoQuotient * y)) < y) {
            let ret = twoQuotient;
        } else {
            let ret = twoQuotient + 1;
        }
        if (xlt0 = ylt0) {
            return ret;
        }
        return -ret;
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, j, yPlus2ToTheJSquared;

        let y = 0;
        let j = n / 2 - 1;
        while (j > -1) {
            let yPlus2ToTheJSquared = (y + powersOfTwo[j]) * (y + powersOfTwo[j]);
            if ((yPlus2ToTheJSquared > 0) & ~(yPlus2ToTheJSquared > x)) {
                let y = y + powersOfTwo[j];
            }
            let j = j - 1;
        }
        return y;
    }

    /** Returns the greater value. */
    function int max(int a, int b) {
        if (a > b) {
            return a;
        } else {
            return b;
        }
    }

    /** Returns the smaller value. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        } else {
            return b;
        }
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            return -x;
        } else {
            return x;
        }
    }
}
